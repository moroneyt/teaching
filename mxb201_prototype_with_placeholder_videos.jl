### A Pluto.jl notebook ###
# v0.11.8

using Markdown
using InteractiveUtils

# This Pluto notebook uses @bind for interactivity. When running this notebook outside of Pluto, the following 'mock version' of @bind gives bound variables a default value (instead of an error).
macro bind(def, element)
    quote
        local el = $(esc(element))
        global $(esc(def)) = Core.applicable(Base.get, el) ? Base.get(el) : missing
        el
    end
end

# â•”â•â•¡ fc40e280-dfca-11ea-2d61-4f9bf0089491
using SymPy, LinearAlgebra

# â•”â•â•¡ 51d2e4a0-def9-11ea-2d08-9b65b288b6d5
using Plots; plotly();

# â•”â•â•¡ 468e6f8e-e040-11ea-36b8-73adb0d967af
using PlutoUI

# â•”â•â•¡ 268d0eb0-deef-11ea-1794-f34d786c7347
include("MXB201.jl")

# â•”â•â•¡ b0a1c500-e20f-11ea-0f41-c113b9e09732
md"![](http://www.thecube.qut.edu.au/img/bg/QUT20.png)"

# â•”â•â•¡ 62c30820-e06c-11ea-0e29-07e6379393f2
md"# MXB201 Lesson X: Orthonormal bases"

# â•”â•â•¡ 99e9afb0-e34c-11ea-2bc0-2321c580f319
md"This is a prototype of using the Pluto system for developing educational resources, loosely based on a lesson from MXB201.  It is to investigate the possiblities, and not intended as an actual lesson.  Videos are all placeholders."

# â•”â•â•¡ b91d76e0-e1a9-11ea-18a2-cb0dbb459373
md"""
### Table of Contents:

  * [The Gram-Schmidt process] (#gs)
  * [Column Vectors](#colvec)
  * [Matrices](#matrix)
  * [Functions](#function)

"""

# â•”â•â•¡ 1a52ea90-e34d-11ea-1640-bd85e47d68ff
md"## Getting set up"

# â•”â•â•¡ 405e0ae0-e072-11ea-2bd4-0ff7d98caa4a
md"Let's include the basics to get us started."

# â•”â•â•¡ f61f0082-e06f-11ea-1226-9beb56fdcc2a
md"We'll need two packages to help us with our Linear Algebra.  `SymPy` is the package we'll use for symbolic manipulation, and `LinearAlgebra` has all the linear algebra operations."

# â•”â•â•¡ 8b728a80-e070-11ea-070d-cd298221c393
md"We will create some symbolic variables that will come in handy."

# â•”â•â•¡ 35241a90-deef-11ea-2be2-dd36b5355dbf
x,Î¾ = symbols("x Î¾", real = true)

# â•”â•â•¡ d64bcf50-e523-11ea-1a43-0f92749af351
md"Before we go further, we should review what we learned last week about inner products.  Watch this short video before continuing."

# â•”â•â•¡ 9721f5b0-e524-11ea-35c2-55630e3a8073
html"""
<iframe width="560" height="315" src="https://www.youtube-nocookie.com/embed/8M6eo3j7jO4" frameborder="0" allow="accelerometer; autoplay; encrypted-media; gyroscope; picture-in-picture" allowfullscreen></iframe>
"""

# â•”â•â•¡ aece01f0-e1b8-11ea-0a5d-efa8c0a3249a
md"""In this lesson we will be interested in the standard inner products for spaces $\mathbb{R}^n$, $\mathcal{M}_{nn}$ and $C[a,b]$.

| Vector Space  | Standard inner product | 
| :------------ |:-----------------------| 
| $u,v \in \mathbb{R}^n$      | $\langle u,v\rangle \ \, = u \cdot v = u^\textrm{T} v$ | 
| $A,B \in \mathcal{M}_{nn}$      | $\langle A,B\rangle = \textrm{vec}(A) \cdot \textrm{vec}(B) = \textrm{tr}(A^\textrm{T} B)$     |   
| $f,g \in C[a,b]$ | $\langle f,g\rangle \ \, = \int_a^b f(\xi)\, g(\xi)\, \textrm{d}\xi$     |    
"""

# â•”â•â•¡ 255d5a70-e072-11ea-06be-5d9d936b10c9
md"Hence we will define methods for the inner product function `ip` for each of: column vectors, matrices and functions."

# â•”â•â•¡ 661d47f0-e072-11ea-0b16-f5fc5449813c
md"An inner product induces a norm via $\|v\| = \sqrt{\langle v,v \rangle}$, so we'll define the function `ipnorm` as well."

# â•”â•â•¡ 40f16410-e34d-11ea-36b5-791219cb8f67
html"<a name=\"gs\"></a>"

# â•”â•â•¡ 3242b182-e34d-11ea-1fc5-ff8ed80a1d4f
md" ## The Gram-Schmidt process"

# â•”â•â•¡ b47c6bd0-e33b-11ea-006b-937efc57db37
md"The [Gram-Schmidt process](https://en.wikipedia.org/wiki/Gram%E2%80%93Schmidt_process) can be used to  can be used to orthonormalise any basis. In words, it iterates over each vector in the basis, subtracting off its projections onto the vectors that came before, and finally normalising.  Watch the video to learn how it works, then we'll continue."

# â•”â•â•¡ 97f409f2-e33b-11ea-2c40-fb7b7e48b94c
html"""<iframe width="560" height="315" src="https://www.youtube-nocookie.com/embed/zHbfZWZJTGc" frameborder="0" allow="accelerometer; autoplay; encrypted-media; gyroscope; picture-in-picture" allowfullscreen></iframe>"""

# â•”â•â•¡ 97eb5f60-e072-11ea-327e-131249e68cb8
md"We'll define a generic method for the `gram_schmidt` function that accepts an array of vectors of any kind.  For convenience, we'll also define a method which accepts column vectors as columns of a matrix."

# â•”â•â•¡ 2516de30-e278-11ea-2227-292a67bf3319
html"<a name=\"colvec\"></a>"

# â•”â•â•¡ 10326df0-e074-11ea-217c-cf86d7259b26
md"## Column Vectors"

# â•”â•â•¡ 57ffbc60-e073-11ea-1064-dfdd142d0704
md"We'll give the algorithm a try on a column vectors first.  Let's repeat the example we did in the video.  We define our three vectors $\textrm{v}_1$, $\textrm{v}_2$, $\textrm{v}_3$."

# â•”â•â•¡ bca52070-e33d-11ea-1600-7194233ff35c
vâ‚ = Sym[1,-1,1]; vâ‚‚ = Sym[0,0,1]; vâ‚ƒ = Sym[1,1,2];

# â•”â•â•¡ dfb93ab0-e33d-11ea-3e3b-f7c9b77149ee
ğ’± = [vâ‚, vâ‚‚, vâ‚ƒ]

# â•”â•â•¡ 37939dc0-e33e-11ea-0a42-a5c6d4ed2ce4
md"Now we apply the Gram-Schmidt process to this set of vectors, to produce an orthonormal basis $\textrm{u}_1$, $\textrm{u}_2$, $\textrm{u}_3$."

# â•”â•â•¡ c8c67bf0-e33e-11ea-3326-4f9beb426494
md"We can confirm that this is an orthonormal basis by verifying that $\langle \textrm{u}_i, \textrm{u}_j \rangle = \delta_{ij}$."

# â•”â•â•¡ 098d8ace-e33e-11ea-3982-339de4e26896
md"An alternative way we could have found this orthonormal basis is to augment the vectors $\textrm{v}_1$, $\textrm{v}_2$, $\textrm{v}_3$ as columns of a matrix $A$."

# â•”â•â•¡ bbe4a572-def1-11ea-2170-6bf128a2c8f6
A = [vâ‚ vâ‚‚ vâ‚ƒ]

# â•”â•â•¡ 7785cde0-e073-11ea-3013-ab2e09e4a6e9
md"The Gram-Schmidt process applied to this matrix produces the following matrix $Q$."

# â•”â•â•¡ 8e162be0-e073-11ea-1b04-6bdb8bcf811b
md"We can confirm it has worked by taking the pair-wise inner products of all columns.  That is, we find $Q^\textrm{T}Q$."

# â•”â•â•¡ a23e02f0-e1b3-11ea-0a6b-9d53cd8aaaca
md"In fact, the matrices $A$ and $Q$ are related by the so-called QR decomposition $A = QR$, where $R$ is an upper triangular matrix."

# â•”â•â•¡ 939486e0-e1bb-11ea-0b44-ab3dda7be737
md"We can confirm that $A = QR$ for this example."

# â•”â•â•¡ d53d3ee0-e349-11ea-0cfc-cd544a20906b
md"So keeping our column vectors in a matrix is more than just a programming convenience.  We have actually discovered a new matrix decomposition -- a most interesting finding!  Let's explore this decomposition in more detail in the video below."

# â•”â•â•¡ b8bad072-e349-11ea-2619-d98de92a8785
html"""
<iframe width="560" height="315" src="https://www.youtube-nocookie.com/embed/FAnNBw7d0vg" frameborder="0" allow="accelerometer; autoplay; encrypted-media; gyroscope; picture-in-picture" allowfullscreen></iframe>
"""

# â•”â•â•¡ 3519d620-e278-11ea-1f72-bd087fb53e81
html"<a name=\"matrix\"></a>"

# â•”â•â•¡ 47fc0bb0-e074-11ea-0e80-5ff7e0420b32
md" ## Matrices"

# â•”â•â•¡ 503e74c0-e074-11ea-39c2-07e5f3daf975
md"We will find an orthonormal basis for the space of $n \times n$ symmetric matrices $\mathcal{S}_n \subset \mathcal{M}_{nn}$.  For this lesson we will take"

# â•”â•â•¡ b92f89b0-e074-11ea-31f7-9954628bb2a0
n = 3

# â•”â•â•¡ e42f3a6e-e074-11ea-3a96-cf1afb508b7d
md"We define a generic matrix $M \in \mathcal{M}_{nn}$."

# â•”â•â•¡ 1bd98c50-def4-11ea-3f90-f3649a2fd2f7
M = symbols.(("a_$i$j" for i = 1:n, j = 1:n), real = true)

# â•”â•â•¡ 053cfd60-e075-11ea-3677-77aad6c2ceca
md"Now we impose symmetry: $M = M^\textrm{T}$.  This implies there must be relationships between the entries $a_{ij}$."

# â•”â•â•¡ 448f8940-def6-11ea-194c-0b0874478811
relationships = solve(vec(M - M'))

# â•”â•â•¡ 5086fa00-e075-11ea-07b7-c370bbabd003
md"We now make these relationships explicit in the matrix $M$, to give our generic symmetric matrix $S$."

# â•”â•â•¡ 4f53e240-def6-11ea-387d-2fb28689f5ae
S = subs.(M, (relationships,))

# â•”â•â•¡ 7a675810-e075-11ea-1269-cf74a34331f0
md"What dimension is the vector space comprising all matrices of the form $S$?  The answer lies in how many free variables $S$ has."

# â•”â•â•¡ 874fb390-def6-11ea-2b34-4f7d0c0447a9
freevars = unique(vcat(free_symbols.(S)...))

# â•”â•â•¡ 3575ea70-def7-11ea-2040-31e0ce47f2d7
dimS = length(freevars)

# â•”â•â•¡ c9069a30-e075-11ea-3737-35eadb921ff2
md"Sequentially setting each free variable to 1, and the remaining free variables to 0, we can construct the standard basis for this space."

# â•”â•â•¡ e805bd00-def7-11ea-1a4d-21dbb701474a
ğ‘º = [subs.(S, (freevars[i].=>eye(Sym,dimS)[i,j] for i = 1:dimS)...) for j = 1:dimS]

# â•”â•â•¡ f5db8020-e075-11ea-11d1-73b8788feed0
md"Now we can use the Gram-Schmidt process to orthonormalise."

# â•”â•â•¡ 13a7aca0-e076-11ea-0d79-e7711e15a35a
md"Again let's check our working by finding all pairwise inner products."

# â•”â•â•¡ c5f6ccb0-e076-11ea-0536-17c5ee317b14
md"Here's a question: what is the \"nearest\" symmetric matrix to an arbitrary matrix $M \in \mathcal{M}_{nn}$?  The answer is given by the projection of $M$ onto $\mathcal{S}_n$.  Let's find what that is.  First we'll find the coordinate vector $c$ of the projection: $c_j = \langle \hat{S}_j, M \rangle$."

# â•”â•â•¡ f9cf3da0-e077-11ea-09dc-e3eabd467304
md"Now we can assemble the projection itself: $M_s = \textrm{Proj}_{\mathcal{S}_{n}}(M) = \displaystyle\sum_{j=1}^{\dim(\mathcal{S})}\, c_j\, \hat{S}_j$"

# â•”â•â•¡ 94edc810-e078-11ea-36a6-fdf19be19145
md"Perhaps unsurprisingly, this is just the so-called \"symmetric part\" of a matrix: $M_s = 
(M + M^\textrm{T})/2$."

# â•”â•â•¡ cdda3740-e1b2-11ea-1569-9dda9b62ac6e
md"If you haven't done so already, go back and change $n$ to be larger, and all the intermediate calculations will update.  You will see that the result is true in general.  Challenge question: can you prove it?"

# â•”â•â•¡ 5a75d860-e2d7-11ea-0807-1da2c2c2df53
md"""#### Exercises

1. Find an orthonormal basis for the space $\mathcal{K}_n$ of _skew-symmetric_ matrices, satisfying $M = -M^\textrm{T}$.
2. Show that $\mathcal{S}_n$ and $\mathcal{K}_n$ are _orthogonal complements_.
3. What is the projection of an arbitrary $n \times n$ matrix onto $\mathcal{K}_n$?
"""

# â•”â•â•¡ 5ece39c0-e34a-11ea-05b4-41c505a95062
md"Try these exercises yourself, but if you're having difficulty you can watch the video below for some hints."

# â•”â•â•¡ 6f60d210-e34b-11ea-1cfd-5d8b60d2c491
html"""
<iframe width="560" height="315" src="https://www.youtube-nocookie.com/embed/uKPmyG18N7I" frameborder="0" allow="accelerometer; autoplay; encrypted-media; gyroscope; picture-in-picture" allowfullscreen></iframe>
"""

# â•”â•â•¡ 6876f200-e278-11ea-1253-f5d4ab68824a
html"<a name=\"function\"></a>"

# â•”â•â•¡ 0a48a8a0-e079-11ea-1899-cb22891a6bec
md" ## Functions"

# â•”â•â•¡ 1a16fc00-e079-11ea-3bff-5f977490e13c
md"A classic application of Gram-Schmidt orthonormalisation for function spaces is in finding the least squares polynomial approximation to a given function.  For example, let's project the function $f(x)$ defined by"

# â•”â•â•¡ f1739bd0-def9-11ea-2935-8b05c72c7a8e
f = exp(x)

# â•”â•â•¡ fd5d45e0-e1b5-11ea-3090-3fa6b780dba3
md"onto the space $\mathcal{P}_N[a,b]$.  We will take the interval $(a,b)$ to be"

# â•”â•â•¡ 050d32f0-e1b6-11ea-0244-476ce7dc7e3b
(a,b) = (-1,1)

# â•”â•â•¡ 006a8680-def0-11ea-2f01-11bc03dc69ca
begin
	ip(u::Vector, v::Vector) = u â‹… v
	ip(A::Matrix, B::Matrix) = vec(A) â‹… vec(B)
	ip(f::Sym, g::Sym) = integrate(f(Î¾)*g(Î¾), (Î¾,a,b))
end

# â•”â•â•¡ 215c35ee-def0-11ea-29a8-ad027acf228b
ipnorm(v) = âˆšip(v,v)

# â•”â•â•¡ 8b591590-def0-11ea-367d-d7e179a18ac8
begin
	function gram_schmidt(B)
		ONB = similar(B) # uninitialised array the same type as B
		ONB[1] = B[1] / ipnorm(B[1]) # first vector just needs normalising
		for i = 2:length(B)
			# subtract projections, then normalise
			ONB[i]  = B[i] - sum(ip(B[i], ONB[j]) * ONB[j] for j = 1:i-1)
			ONB[i] /= ipnorm(ONB[i])
		end
		return ONB
	end

	# We'll add a method for taking the basis vectors as the columns of a matrix
	function gram_schmidt(A::Matrix)
		# forward to the generic method
		hcat(gram_schmidt([A[:,j] for j = 1:size(A,2)])...)
	end
end	

# â•”â•â•¡ f7aad660-e33d-11ea-26f0-dfcd9458d0bd
ğ’° = gram_schmidt(ğ’±)

# â•”â•â•¡ a96f1b90-e33e-11ea-06e2-af17c0ce199a
[ip(ğ’°[i], ğ’°[j]) for i = 1:3,  j = 1:3]

# â•”â•â•¡ bee00b1e-def1-11ea-27c3-e37601db9a5e
Q = gram_schmidt(A)

# â•”â•â•¡ 9bb458d0-e073-11ea-1481-1d79fc895bcb
Q'Q

# â•”â•â•¡ 9a415570-e1b3-11ea-2beb-85d2e537c70a
R = Q'A

# â•”â•â•¡ c248fbe0-e1b3-11ea-0638-97c0b32e61c9
A == Q*R

# â•”â•â•¡ 5e0f5420-def8-11ea-155e-85a16dce418f
ğ‘ºâ‚’â‚™ = gram_schmidt(ğ‘º)

# â•”â•â•¡ 1fc442a0-e076-11ea-3ade-4da1b7930d82
[ip(ğ‘ºâ‚’â‚™[i], ğ‘ºâ‚’â‚™[j]) for i = 1:dimS, j = 1:dimS]

# â•”â•â•¡ dd5c97ae-def8-11ea-130e-f33e93308e63
c = [ip(ğ‘ºâ‚’â‚™[j], M) for j = 1:dimS]

# â•”â•â•¡ 16320020-def9-11ea-0619-95f00f27b1fd
Mâ‚› = simplify.(sum(c[j] * ğ‘ºâ‚’â‚™[j] for j = 1:dimS))

# â•”â•â•¡ c5a16ed0-e078-11ea-194a-ff3f6277ec0c
Mâ‚› == (M+M')/2

# â•”â•â•¡ 6561bbf0-e079-11ea-3d3c-d3c14223eaab
md"We'll need an orthonormal basis for $\mathcal{P}_N[a,b]$.  We can get one by orthonormalising the basis of monomials $x^p$ for $p = 0\ldots N$.  Use the slider to choose the value of $N$ you want."

# â•”â•â•¡ b2916880-e079-11ea-008e-ff10311bda02
@bind N Slider(2:9; default=3)

# â•”â•â•¡ 0c6d2790-e07f-11ea-195b-a329dd163f76
md" $N$ = $N"

# â•”â•â•¡ 5d229e20-e040-11ea-0ea7-abfbaae6f1c0
ğ“Ÿ = x.^(0:N)

# â•”â•â•¡ 88137570-e07a-11ea-0b8f-7ffca9055f24
md"Now apply the Gram-Schmidt process as usual."

# â•”â•â•¡ a52812be-e06f-11ea-2fb7-ab3c711814c3
ğ“Ÿâ‚’â‚™ = gram_schmidt(ğ“Ÿ)

# â•”â•â•¡ 222a1730-e1b7-11ea-24a5-3bab3e1fc3b0
md"The orthonormal polynomials we've just constructed are the famous [Legendre polynomials](https://en.wikipedia.org/wiki/Legendre_polynomials)."

# â•”â•â•¡ c2f0b672-e2d8-11ea-0237-7dae000c34aa
plot(ğ“Ÿâ‚’â‚™, -1, 1, xlab = "ğ‘¥", title="Legendre polynomials",
	label="P".*string.((0:N)'))

# â•”â•â•¡ 945a64e0-e2ed-11ea-3ac0-bb8cd6c9b448
md"We can confirm they are indeed orthonormal over $[-1,1]$."

# â•”â•â•¡ 9fd45240-e2ed-11ea-24bd-9783df8c385c
ip.(ğ“Ÿâ‚’â‚™', ğ“Ÿâ‚’â‚™) == eye(Sym, N+1)

# â•”â•â•¡ a502d680-e07a-11ea-0218-bb8139f7763b
md"Now find the coordinates of the projection of $f$ onto this basis."

# â•”â•â•¡ 0e4a92e0-defa-11ea-1153-7161a1571afa
ğ‘ = ip.(f, ğ“Ÿâ‚’â‚™)

# â•”â•â•¡ d7af18f0-e07a-11ea-080c-a7cc070c16eb
md"With this coordinates we can build the projection itself."

# â•”â•â•¡ 1fe8c4c0-e02d-11ea-011d-1748624063ef
ğ‘ƒf = sum(ğ‘[i] * ğ“Ÿâ‚’â‚™[i] for i = 1:N+1)

# â•”â•â•¡ 197070e0-e07b-11ea-1e94-87e8fcbfe6e1
md"We can plot $f$ and its projection $\mathcal{P}f$ on the same figure to compare."

# â•”â•â•¡ 834cae20-e044-11ea-292d-99162a87ca93
plot([f,ğ‘ƒf], a, b; label=["ğ‘“" "ğ‘ƒğ‘“"], xlabel="ğ‘¥",
	title="Legendre projection of f(x) = $f with N=$N")

# â•”â•â•¡ f0314640-e1b6-11ea-3d91-b3837ee652dd
md"If you like, go back and adjust the slider to increase $N$ and improve the accuracy.  Or to make it a harder problem, change the function $f$ to something more interesting!  **n.b.** You _might_ be tempted to change the interval to something wider than $[-1,1]$ to make it more challenging, but resist that urge for now, because the next section on trigonometric polynomials assumes that the interval is $[-1,1]$."

# â•”â•â•¡ 4c930430-e1cd-11ea-3189-0b10e5f6aa61
md" ### Trigonometric polynomials"

# â•”â•â•¡ 5e5622b0-e1cd-11ea-0040-9d6580583765
md"""A [trigonometric polynomial](https://en.wikipedia.org/wiki/Trigonometric_polynomial) is a linear combination of the trigonometric functions $\cos(k\pi x)$ and $\sin(k\pi x)$

$T_N(x) = \sum_{k=0}^N a_k \cos(k\pi x) + \sum_{k=1}^N b_k \sin(k \pi x).$

Other sources often use simply $\cos(kx)$ and $\sin(kx)$ but we will prefer our definition so that this function is periodic on the interval $[-1,1]$ (rather than $[-\pi,\pi]$).

A natural question is what is the orthogonal projection of an arbitrary periodic function on $[-1,1]$ onto the span of these trigonometric functions?  In other words, what is the best trigonometric polynomial approximation to an arbitrary periodic function?

Let's start small, we'll take
"""

# â•”â•â•¡ d4f67430-e1d0-11ea-32be-1b630e0b5b16
ğ‘ = 3

# â•”â•â•¡ bcbf55ee-e1ce-11ea-0f4e-d5f3831b4cac
md"We will build our basis $\mathcal{T}$ of trigonometric functions, with the plan to orthonormalise using the Gram-Schmidt process."

# â•”â•â•¡ 84a535f0-e1c8-11ea-109e-116b91fa310d
ğ’¯ = [cos.((0:ğ‘)*(Ï€*x)); sin.((1:ğ‘)*(Ï€*x))]

# â•”â•â•¡ dc6b3ef0-e1ce-11ea-1e55-7f669c122e9c
md"But we are in for a surprise!  This basis is _already_ orthogonal on $[-1,1]$.  In fact, it's very nearly an ortho_normal_ basis, as we can readily confirm by finding the pairwise inner products:"

# â•”â•â•¡ 69223950-e1cc-11ea-0cfb-a1fbaa4258b8
ip.(ğ’¯', ğ’¯)

# â•”â•â•¡ 28d29270-e1cf-11ea-27bd-5550b53955cc
md"Only the constant term needs scaling in order to complete our orthonormal basis."

# â•”â•â•¡ 41ead3d0-e1cf-11ea-160f-ef73c7a53ccc
ğ’¯[1] = 1/âˆšSym(2); ğ’¯

# â•”â•â•¡ 95af2780-e1d1-11ea-1320-7b07b566db1a
md"This is now an orthonormal basis on $[-1,1]$, as we can readily confirm."

# â•”â•â•¡ 79c78460-e1cf-11ea-3da9-4382ba235a4c
ip.(ğ’¯', ğ’¯) == eye(Sym, 2*ğ‘+1)

# â•”â•â•¡ 96e52160-e1cf-11ea-2047-1711a70f7e24
md"With our orthonormal basis in hand, we can merrily project whatever function we wish onto this space.  Let's take it simple, and project the function $g(x)$ defined by"

# â•”â•â•¡ 041d3630-e1cd-11ea-36d1-152488d6a560
g = x

# â•”â•â•¡ eb234ae0-e1cf-11ea-023e-6fa79f001f77
md"We find the coefficients $C_j = \langle g, \mathcal{T}_j \rangle$."

# â•”â•â•¡ c9ab0650-e1cf-11ea-1a8e-bd10481c85e2
ğ¶ = ip.(g, ğ’¯)

# â•”â•â•¡ dee215e0-e1cf-11ea-08a2-c9c9b37e9089
md"And now the projection $\mathcal{P}g$."

# â•”â•â•¡ 07a82a50-e1d0-11ea-0d33-3129c17f140b
ğ‘ƒg = ğ¶' * ğ’¯

# â•”â•â•¡ 312077c0-e1d0-11ea-1cb1-ed63dfbade65
md"How do the two functions compare?"

# â•”â•â•¡ 3564ff40-e1d0-11ea-1b14-8fd79d4daf9c
plot([g,ğ‘ƒg], -1,1; label=["ğ‘”" "ğ‘ƒğ‘”"], xlabel="ğ‘¥",
	title="Trigonometric projection of g(x) = $g with N = $ğ‘")

# â•”â•â•¡ b91ab090-e1d1-11ea-26ab-c732289e9fee
md"But wait, $g(x)$ _isn't_ a periodic function. So what exactly have we built here?  Let's extend our axis limits a little further."

# â•”â•â•¡ d100da92-e1d1-11ea-0448-e3029fcc0999
plot(ğ‘ƒg, -2,2; label = "ğ‘ƒğ‘”", xlabel="ğ‘¥",
	title="Trigonometric projection of g(x) = $g with N = $ğ‘")

# â•”â•â•¡ d5628700-e1d1-11ea-23cb-75461ca5ebc6
md"""We have in fact constructed a trigonometric polynomial approximating the _periodic extension_ of $g(x)$.  Interestingly, this periodic extension is not actually a continuous function, yet our trigonometric polynomial is doing a pretty decent job of handling the discontinuities at $\pm 1$.

Let's build a little function to do this projection efficiently, so that we can explore the effect of increasing $N$.
"""

# â•”â•â•¡ 3717f480-e1d2-11ea-3d28-fd9bb0655e43
function trig_poly(g, ğ‘)
	ğ’¯ = [cos.((0:ğ‘)*(Ï€*x)); sin.((1:ğ‘)*(Ï€*x))]
	ğ’¯[1] /= âˆš2
	ğ¶ = ip.(g, ğ’¯)
	ğ‘ƒg = ğ¶' * ğ’¯
end

# â•”â•â•¡ 805ad680-e1d2-11ea-21ea-eb346ac094fe
md"Quick check that it produces the same result we had before:"

# â•”â•â•¡ 62604700-e1d2-11ea-1ceb-b9c41829fd57
trig_poly(g, ğ‘) == ğ‘ƒg

# â•”â•â•¡ 7e84de00-e1d2-11ea-1da2-3995db8a1565
md"Now we can play with the upper limit (let's call it $N_\textrm{up}$) and see what effect it has on the figure."

# â•”â•â•¡ 668d9d00-e1d2-11ea-151a-89fa882ccccd
@bind ğ‘áµ¤â‚š Slider(2:50)

# â•”â•â•¡ b4074ae0-e1d2-11ea-26bf-abb7515e269d
md" $N_\textrm{up}$ = $ğ‘áµ¤â‚š"

# â•”â•â•¡ c707cb60-e1d2-11ea-17f8-cf5a22da41ae
plot(trig_poly(g, ğ‘áµ¤â‚š), -2,2; label = "ğ‘ƒğ‘”", xlabel="ğ‘¥",
	title="Trigonometric projection of g(x) = $g with N = $ğ‘áµ¤â‚š")

# â•”â•â•¡ 1d7d6e50-e1d3-11ea-1903-f1a33677c13f
md"If you experiment with larger values of $N_\textrm{up}$ you'll see that the projection always overshoots at the discontinuity (pay attention to the y-axis limits -- they go beyond $[-1,1]$).  This is known as the [Gibbs phenomenon](https://en.wikipedia.org/wiki/Gibbs_phenomenon)."

# â•”â•â•¡ 070b5880-e359-11ea-3fb2-0b1231d37f49
md"""#### Exercises

1. Devise a number of interesting "test functions" with the goal of projecting them onto polynomial and trigonometric polynomial spaces of various dimensions.  Include at least one example of a:
* discontinuous function
* function without a continuous derivative
* smooth function
* periodic function
Write a Julia function to carry out both projections for a given mathematical function $f(x)$ and plot the resulting projections on the same axis.  Record your observations of the behaviour of the projections of these functions as you increase the number of elements in the bases.
"""

# â•”â•â•¡ ee8a64a0-e2d5-11ea-2eb2-01dbd7ad1031
md" ### Approximation theory and Fourier analysis"

# â•”â•â•¡ ffd67e60-e2d5-11ea-18a1-799473ea8f58
md"""
You may be wondering, what function do these projections _converge_ to in the limit as $N \to \infty$?  Do they converge at all?  Does they only converge sometimes?  Does this depend on the function?  Is the limit always the same as the function?  If not, why not, and when?  All very good questions of course!  And in fact, many of them turn out to have non-trival answers.  The study of questions such as these is the subject of [Approximation theory](https://en.wikipedia.org/wiki/Approximation_theory) (in the case of polynomial projections) and [Fourier analysis](https://en.wikipedia.org/wiki/Fourier_analysis) (in the case of trigonometric projections).  Answers to these questions are, however, beyond the scope of this unit, because they first require a rigorous understanding of what \"converge\" actually means in the context of functions -- an _analytic_ concept that would take us beyond the realm of linear _algebra_.

Still, to get a taste of where this line of thinking can lead in the case of trigonometric projections, as well as the historical motivation that led to its discovery, you could do worse than watching this highly entertaining video on the topic of [Fourier series](https://en.wikipedia.org/wiki/Fourier_series).
"""

# â•”â•â•¡ fca08e64-e2d2-11ea-2a0e-49833672880a
html"""
<iframe width="560" height="315" src="https://www.youtube-nocookie.com/embed/r6sGWTCMz2k" frameborder="0" allow="accelerometer; autoplay; encrypted-media; gyroscope; picture-in-picture" allowfullscreen></iframe>
"""

# â•”â•â•¡ Cell order:
# â•Ÿâ”€b0a1c500-e20f-11ea-0f41-c113b9e09732
# â•Ÿâ”€62c30820-e06c-11ea-0e29-07e6379393f2
# â•Ÿâ”€99e9afb0-e34c-11ea-2bc0-2321c580f319
# â•Ÿâ”€b91d76e0-e1a9-11ea-18a2-cb0dbb459373
# â•Ÿâ”€1a52ea90-e34d-11ea-1640-bd85e47d68ff
# â•Ÿâ”€405e0ae0-e072-11ea-2bd4-0ff7d98caa4a
# â• â•268d0eb0-deef-11ea-1794-f34d786c7347
# â•Ÿâ”€f61f0082-e06f-11ea-1226-9beb56fdcc2a
# â• â•fc40e280-dfca-11ea-2d61-4f9bf0089491
# â•Ÿâ”€8b728a80-e070-11ea-070d-cd298221c393
# â• â•35241a90-deef-11ea-2be2-dd36b5355dbf
# â•Ÿâ”€d64bcf50-e523-11ea-1a43-0f92749af351
# â•Ÿâ”€9721f5b0-e524-11ea-35c2-55630e3a8073
# â•Ÿâ”€aece01f0-e1b8-11ea-0a5d-efa8c0a3249a
# â•Ÿâ”€255d5a70-e072-11ea-06be-5d9d936b10c9
# â• â•006a8680-def0-11ea-2f01-11bc03dc69ca
# â•Ÿâ”€661d47f0-e072-11ea-0b16-f5fc5449813c
# â• â•215c35ee-def0-11ea-29a8-ad027acf228b
# â•Ÿâ”€40f16410-e34d-11ea-36b5-791219cb8f67
# â•Ÿâ”€3242b182-e34d-11ea-1fc5-ff8ed80a1d4f
# â•Ÿâ”€b47c6bd0-e33b-11ea-006b-937efc57db37
# â•Ÿâ”€97f409f2-e33b-11ea-2c40-fb7b7e48b94c
# â•Ÿâ”€97eb5f60-e072-11ea-327e-131249e68cb8
# â• â•8b591590-def0-11ea-367d-d7e179a18ac8
# â•Ÿâ”€2516de30-e278-11ea-2227-292a67bf3319
# â•Ÿâ”€10326df0-e074-11ea-217c-cf86d7259b26
# â•Ÿâ”€57ffbc60-e073-11ea-1064-dfdd142d0704
# â• â•bca52070-e33d-11ea-1600-7194233ff35c
# â• â•dfb93ab0-e33d-11ea-3e3b-f7c9b77149ee
# â•Ÿâ”€37939dc0-e33e-11ea-0a42-a5c6d4ed2ce4
# â• â•f7aad660-e33d-11ea-26f0-dfcd9458d0bd
# â•Ÿâ”€c8c67bf0-e33e-11ea-3326-4f9beb426494
# â• â•a96f1b90-e33e-11ea-06e2-af17c0ce199a
# â•Ÿâ”€098d8ace-e33e-11ea-3982-339de4e26896
# â• â•bbe4a572-def1-11ea-2170-6bf128a2c8f6
# â•Ÿâ”€7785cde0-e073-11ea-3013-ab2e09e4a6e9
# â• â•bee00b1e-def1-11ea-27c3-e37601db9a5e
# â•Ÿâ”€8e162be0-e073-11ea-1b04-6bdb8bcf811b
# â• â•9bb458d0-e073-11ea-1481-1d79fc895bcb
# â•Ÿâ”€a23e02f0-e1b3-11ea-0a6b-9d53cd8aaaca
# â• â•9a415570-e1b3-11ea-2beb-85d2e537c70a
# â•Ÿâ”€939486e0-e1bb-11ea-0b44-ab3dda7be737
# â• â•c248fbe0-e1b3-11ea-0638-97c0b32e61c9
# â•Ÿâ”€d53d3ee0-e349-11ea-0cfc-cd544a20906b
# â•Ÿâ”€b8bad072-e349-11ea-2619-d98de92a8785
# â•Ÿâ”€3519d620-e278-11ea-1f72-bd087fb53e81
# â•Ÿâ”€47fc0bb0-e074-11ea-0e80-5ff7e0420b32
# â•Ÿâ”€503e74c0-e074-11ea-39c2-07e5f3daf975
# â• â•b92f89b0-e074-11ea-31f7-9954628bb2a0
# â•Ÿâ”€e42f3a6e-e074-11ea-3a96-cf1afb508b7d
# â• â•1bd98c50-def4-11ea-3f90-f3649a2fd2f7
# â•Ÿâ”€053cfd60-e075-11ea-3677-77aad6c2ceca
# â• â•448f8940-def6-11ea-194c-0b0874478811
# â•Ÿâ”€5086fa00-e075-11ea-07b7-c370bbabd003
# â• â•4f53e240-def6-11ea-387d-2fb28689f5ae
# â•Ÿâ”€7a675810-e075-11ea-1269-cf74a34331f0
# â• â•874fb390-def6-11ea-2b34-4f7d0c0447a9
# â• â•3575ea70-def7-11ea-2040-31e0ce47f2d7
# â•Ÿâ”€c9069a30-e075-11ea-3737-35eadb921ff2
# â• â•e805bd00-def7-11ea-1a4d-21dbb701474a
# â•Ÿâ”€f5db8020-e075-11ea-11d1-73b8788feed0
# â• â•5e0f5420-def8-11ea-155e-85a16dce418f
# â•Ÿâ”€13a7aca0-e076-11ea-0d79-e7711e15a35a
# â• â•1fc442a0-e076-11ea-3ade-4da1b7930d82
# â•Ÿâ”€c5f6ccb0-e076-11ea-0536-17c5ee317b14
# â• â•dd5c97ae-def8-11ea-130e-f33e93308e63
# â•Ÿâ”€f9cf3da0-e077-11ea-09dc-e3eabd467304
# â• â•16320020-def9-11ea-0619-95f00f27b1fd
# â•Ÿâ”€94edc810-e078-11ea-36a6-fdf19be19145
# â• â•c5a16ed0-e078-11ea-194a-ff3f6277ec0c
# â•Ÿâ”€cdda3740-e1b2-11ea-1569-9dda9b62ac6e
# â•Ÿâ”€5a75d860-e2d7-11ea-0807-1da2c2c2df53
# â•Ÿâ”€5ece39c0-e34a-11ea-05b4-41c505a95062
# â•Ÿâ”€6f60d210-e34b-11ea-1cfd-5d8b60d2c491
# â•Ÿâ”€6876f200-e278-11ea-1253-f5d4ab68824a
# â•Ÿâ”€0a48a8a0-e079-11ea-1899-cb22891a6bec
# â•Ÿâ”€1a16fc00-e079-11ea-3bff-5f977490e13c
# â• â•f1739bd0-def9-11ea-2935-8b05c72c7a8e
# â•Ÿâ”€fd5d45e0-e1b5-11ea-3090-3fa6b780dba3
# â• â•050d32f0-e1b6-11ea-0244-476ce7dc7e3b
# â•Ÿâ”€6561bbf0-e079-11ea-3d3c-d3c14223eaab
# â•Ÿâ”€b2916880-e079-11ea-008e-ff10311bda02
# â•Ÿâ”€0c6d2790-e07f-11ea-195b-a329dd163f76
# â• â•5d229e20-e040-11ea-0ea7-abfbaae6f1c0
# â•Ÿâ”€88137570-e07a-11ea-0b8f-7ffca9055f24
# â• â•a52812be-e06f-11ea-2fb7-ab3c711814c3
# â•Ÿâ”€222a1730-e1b7-11ea-24a5-3bab3e1fc3b0
# â• â•51d2e4a0-def9-11ea-2d08-9b65b288b6d5
# â• â•c2f0b672-e2d8-11ea-0237-7dae000c34aa
# â•Ÿâ”€945a64e0-e2ed-11ea-3ac0-bb8cd6c9b448
# â• â•9fd45240-e2ed-11ea-24bd-9783df8c385c
# â•Ÿâ”€a502d680-e07a-11ea-0218-bb8139f7763b
# â• â•0e4a92e0-defa-11ea-1153-7161a1571afa
# â•Ÿâ”€d7af18f0-e07a-11ea-080c-a7cc070c16eb
# â• â•1fe8c4c0-e02d-11ea-011d-1748624063ef
# â•Ÿâ”€197070e0-e07b-11ea-1e94-87e8fcbfe6e1
# â• â•834cae20-e044-11ea-292d-99162a87ca93
# â•Ÿâ”€f0314640-e1b6-11ea-3d91-b3837ee652dd
# â•Ÿâ”€4c930430-e1cd-11ea-3189-0b10e5f6aa61
# â•Ÿâ”€5e5622b0-e1cd-11ea-0040-9d6580583765
# â• â•d4f67430-e1d0-11ea-32be-1b630e0b5b16
# â•Ÿâ”€bcbf55ee-e1ce-11ea-0f4e-d5f3831b4cac
# â• â•84a535f0-e1c8-11ea-109e-116b91fa310d
# â•Ÿâ”€dc6b3ef0-e1ce-11ea-1e55-7f669c122e9c
# â• â•69223950-e1cc-11ea-0cfb-a1fbaa4258b8
# â•Ÿâ”€28d29270-e1cf-11ea-27bd-5550b53955cc
# â• â•41ead3d0-e1cf-11ea-160f-ef73c7a53ccc
# â•Ÿâ”€95af2780-e1d1-11ea-1320-7b07b566db1a
# â• â•79c78460-e1cf-11ea-3da9-4382ba235a4c
# â•Ÿâ”€96e52160-e1cf-11ea-2047-1711a70f7e24
# â• â•041d3630-e1cd-11ea-36d1-152488d6a560
# â•Ÿâ”€eb234ae0-e1cf-11ea-023e-6fa79f001f77
# â• â•c9ab0650-e1cf-11ea-1a8e-bd10481c85e2
# â•Ÿâ”€dee215e0-e1cf-11ea-08a2-c9c9b37e9089
# â• â•07a82a50-e1d0-11ea-0d33-3129c17f140b
# â•Ÿâ”€312077c0-e1d0-11ea-1cb1-ed63dfbade65
# â• â•3564ff40-e1d0-11ea-1b14-8fd79d4daf9c
# â•Ÿâ”€b91ab090-e1d1-11ea-26ab-c732289e9fee
# â• â•d100da92-e1d1-11ea-0448-e3029fcc0999
# â•Ÿâ”€d5628700-e1d1-11ea-23cb-75461ca5ebc6
# â• â•3717f480-e1d2-11ea-3d28-fd9bb0655e43
# â•Ÿâ”€805ad680-e1d2-11ea-21ea-eb346ac094fe
# â• â•62604700-e1d2-11ea-1ceb-b9c41829fd57
# â•Ÿâ”€7e84de00-e1d2-11ea-1da2-3995db8a1565
# â•Ÿâ”€668d9d00-e1d2-11ea-151a-89fa882ccccd
# â•Ÿâ”€b4074ae0-e1d2-11ea-26bf-abb7515e269d
# â• â•c707cb60-e1d2-11ea-17f8-cf5a22da41ae
# â•Ÿâ”€1d7d6e50-e1d3-11ea-1903-f1a33677c13f
# â•Ÿâ”€070b5880-e359-11ea-3fb2-0b1231d37f49
# â•Ÿâ”€ee8a64a0-e2d5-11ea-2eb2-01dbd7ad1031
# â•Ÿâ”€ffd67e60-e2d5-11ea-18a1-799473ea8f58
# â•Ÿâ”€fca08e64-e2d2-11ea-2a0e-49833672880a
# â•Ÿâ”€468e6f8e-e040-11ea-36b8-73adb0d967af
